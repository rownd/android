apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

if (PUBLISH_VERSION == null) {
    throw new IllegalArgumentException("Variable 'PUBLISH_VERSION' is null. Cannot continue.")
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                components.getByName('release')
                // The coordinates of the library, being set from variables that
                // we'll set up later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
//                artifact javadocJar

                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Rownd SDK for Android'
                    url = 'https://github.com/rownd/android'
                    licenses {
                        license {
                            name = 'Apache 2.0 License'
                            url = 'https://github.com/rownd/android/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'rownd'
                            name = 'Rownd'
                            email = 'support@rownd.io'
                        }
                        developer {
                            id = 'mhamann'
                            name = 'Matt Hamann'
                            email = 'mhamann@rownd.io'
                        }
                        // Add all other devs here...
                    }

                    // Version control info - if you're using GitHub, follow the
                    // format as seen here
                    scm {
                        connection = 'scm:git:github.com/rownd/android.git'
                        developerConnection = 'scm:git:ssh://github.com/rownd/android.git'
                        url = 'https://github.com/rownd/android/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}
